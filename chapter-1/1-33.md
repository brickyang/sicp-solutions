# 练习 1.33

略。

---

## filtered-accumulate

```scheme
(define (filtered-accumulate combiner null-value term a next b filter)
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a)
              (if (filter a)
                  (combiner (term a) result)
                  result))))
  (iter a null-value))
```

## 素数之和

```scheme
; 假设 prime? 已实现

(define (sum term a next b filter)
  (define (combiner a b) (+ a b))
  (define null-value 0)
  (filtered-accumulate combiner null-value term a next b filter))

(define (sum-primes a b)
  (define (term x) x)
  (define (next x) (+ x 1))
  (define (filter a) (prime? a))
  (sum term a next b filter))
```

## 互素数之积

```scheme
(define (coprime? i n)
  (and (< i n) (= 1 (gcd i n))))

(define (product term a next b filter)
  (define (combiner a b) (* a b))
  (define null-value 1)
  (filtered-accumulate combiner null-value term a next b filter))

(define (product-coprimes n)
  (define (term x) x)
  (define (next x) (+ x 1))
  (define (filter a) (coprime? a n))
  (product term 1 next n filter))
```
